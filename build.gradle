buildscript { 
    ext.kotlin_version = '1.3.61'

    ext.versions = [
            'compileSdk' : 28,
            'minSdk'     : 23,
            'targetSdk'  : 28,
            'versionName': '1.0.0',
            'versionCode': 1001
    ]

    ext.deps = [
            'kotlin'             : '1.3.61',
            'ktLint'             : '9.0.0',
            'publisher'          : '2.6.2',
            'dokka'              : '0.9.18',
            'ktlint'             : '0.24.0',
            'dagger'             : '2.25.2',
            'assisted'           : '0.5.2',
            'okhttp'             : '4.2.2',
            'gson'               : '2.8.6',
            'retrofit'           : '2.7.0',
            'rxAndroid'          : '2.1.1',
            'rxJava'             : '2.2.13',
            'lottie'             : '3.1.0',
            'rxKotlin'           : '2.4.0',
            'appcompat'          : '1.1.0',
            'androidx'           : '1.0.0',
            'crypto'             : '1.0.0-alpha02',
            'androidxCollection' : '1.0.0',
            'androidxCoreRuntime': '2.0.1-alpha01',
            'androidxArch'       : '2.0.0',
            'constraintLayout'   : '2.0.0-beta3',
            'coreKtx'            : '1.0.0',
            'glide'              : '4.9.0',
            'lifecycle'          : '2.1.0-alpha01',
            'material'           : '1.1.0-alpha09',
            'room'               : '2.2.2',
            'supportLibrary'     : '28.0.0',
            'viewPager2'         : '1.0.0-alpha02',
            'workManager'        : '2.2.0',
            'paging'             : '2.1.0',
            'robolectric'        : '4.3',
            'mockito'            : '2.23.0',
            'kmock'              : '2.0.0-RC3',
            'junit'              : '4.12',
            'androidxTestCore'   : '1.2.1-alpha02',
            'espresso'           : '3.1.0-beta02',
            'extJunit'           : '1.1.0',
            'testRules'          : '1.1.0-beta02',
            'gTruth'             : '1.0',
            'permissions'        : '3.0.0'
    ]

    ext.names = [
            'applicationId': 'com.forestgump.boilerplate'
    ]

    ext.util = [
            'gitHash'   : { ->
                return 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
            },
            'gitVersion': { ->
                return 100 + Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
            }
    ]

    repositories {
        google()
        jcenter()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${deps.kotlin}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${deps.dokka}"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:${deps.ktLint}"
        classpath "com.github.triplet.gradle:play-publisher:${deps.publisher}"
    }
}

plugins {
    /**
     * CAUTION!!! Gradle does not support variables for plugins so be sure to
     * update this version as well, if you update ktlint
     */
    id "org.jlleitschuh.gradle.ktlint" version "9.0.0"
    id "com.github.ben-manes.versions" version "0.27.0"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}
apply from: file("test_reports.gradle")

subprojects {

    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    apply plugin: 'com.github.triplet.play'

    ktlint {
        debug = true
        verbose = true
        android = true
        outputToConsole = true
        filter {
            exclude("**/generated/**")
            include("**/kotlin/**")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
